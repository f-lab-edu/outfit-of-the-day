plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id "jacoco"
}

ext {
    snippetsDir = file('build/generated-snippets') // 전역 변수로 snippet을 저장할 directory를 설정한다.
}

java {
    sourceCompatibility = '17'
}

jacoco {
    toolVersion "0.8.8"
    reportsDirectory = layout.buildDirectory.dir("reports/jacoco")
}

group = 'study'
version = '0.0.1-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt // asciidoctor extension에 대한 설정을 넣어준다.
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    implementation 'mysql:mysql-connector-java:8.0.23'
}


jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = false
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }

            excludes = [
                    '*.*Application',
                    '*.*ExceptionHandler',
                    '**.util.**'
            ]
        }
    }
}
jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        html.outputLocation = file("$buildDir/jacoco/index.html")
        // file("$buildDir/myOutput.txt")
    }
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            '**/*Application*',
                            '**/*Exception*',
                            '**/request/**',
                            '**/response/**',
                            '**/util/**'
                    ])
                })
        )
    }

}

asciidoctor {
    dependsOn test
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'

    dependsOn test // test -> asciidoctor 순으로 테스크가 수행된다.

}

// bootJar: 실행 가능한 *.jar 파일을 만드는 역할을 수행한다.
bootJar {
    dependsOn asciidoctor // test -> asciidoctor -> bootJar 순으로 task가 수행된다.

    // asciidoctor.outputDir: '/build/docs/asciidocs'
    from("${asciidoctor.outputDir}") {
        into 'static/docs' // asciidoctor.outputDir 폴더에 있는 파일을
    }
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir // test가 끝난 결과물을 snippetsDir에 저장한다.
    finalizedBy jacocoTestReport // 테스트 종료 이후에 레포트를 생성
    finalizedBy 'jacocoTestCoverageVerification' // 코드 커버리지가 규칙을 통과하지 않으면 빌드를 실패시킨다.
}



